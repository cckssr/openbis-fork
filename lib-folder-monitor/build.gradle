apply from: '../build/javalibrary.gradle'

evaluationDependsOn(':api-openbis-java')

dependencies {
    annotationProcessor 'lombok:lombok:1.18.38'
    implementation project(':lib-commonbase'), project(':api-openbis-java'), 'lombok:lombok:1.18.38'
    testImplementation 'testng:testng:6.8-CISD'
}

task folderMonitorZip(type: Zip, dependsOn: compileJava) {
    archiveBaseName = 'folder-monitor'
    from(jar) {
        into('folder-monitor/lib/')
    }
    from(configurations.runtimeClasspath) {
        into('folder-monitor/lib/')
    }
    from('dist/etc') {
        into 'folder-monitor/etc'
    }
    from('dist/bin') {
        filePermissions {
            user {
                execute = true
            }
        }
        into 'folder-monitor/bin'
    }
}

task folderMonitorZipWithV3Batteries(type: Zip, dependsOn: [project(':api-openbis-java').tasks.jarWithDependencies]){
    archiveBaseName ='folder-monitor-with-batteries'

    from(zipTree(folderMonitorZip.archiveFile)) {
    }

    from(project(':api-openbis-java').tasks.jarWithDependencies.archiveFile){
        into "folder-monitor/lib"
    }
}