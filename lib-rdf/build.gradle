plugins {
    id 'java'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

repositories {
    ivy {
        ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
        artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.apache.jena:apache-jena-libs:5.0.0',
            'apache:poi:3.17',
            'apache:poi-ooxml:3.17',
            'apache:poi-ooxml-schemas:3.17',
            'commons-cli:commons-cli:1.4'
    implementation project(':api-openbis-java')

    testImplementation 'junit:junit:4.13.1'
}

tasks.register('buildRdfTool', Jar) {
    zip64 = true
    dependsOn tasks.build
    duplicatesStrategy 'include'

    archiveBaseName.set('lib-rdf-tool')
    includeEmptyDirs = false

    manifest {
        attributes(
                'Main-Class': 'ch.ethz.sis.rdf.main.RDFCommandLine'
        )
    }

    from(zipTree(tasks.jar.outputs.files.singleFile)) {
        include '**/*'
    }

    from(configurations.runtimeClasspath.collect { zipTree(it) }) {
        include '**/*'
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
}