plugins {
    id 'java-library'
    id 'application'
    id 'com.google.protobuf' version '0.9.5'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.74.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

def baseName =  'app-openbis-drive'
def implementationVersion = '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx.version = "21.0.6"

evaluationDependsOn(':lib-json')
evaluationDependsOn(':api-data-store-server-java')

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

//tasks.register('compileAvroSchemas', Exec) {
//    executable "java"
//    args "-jar",
//            "util/avro-tools-1.12.0.jar",
//            "compile",
//            "protocol",
//            "./src/main/resources/avro/drive-api.avpr",
//            "./src/main/java"
//}
//
//build {
//    dependsOn("compileAvroSchemas")
//}

repositories {
    ivy {
        ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
        artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    compileOnly("org.projectlombok:lombok:1.18.38")
    testCompileOnly("org.projectlombok:lombok:1.18.38")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.38")

    api(project(":lib-json"))
    api(project(":api-openbis-java"))

    implementation("org.xerial:sqlite-jdbc:3.50.2.0")
    implementation("commons-cli:commons-cli:1.10.0")
    implementation("slf4j:slf4j-api:1.7.24")

    //GOOGLE PROTOBUF
    implementation("com.google.protobuf:protobuf-java:4.31.1")
    implementation("io.grpc:grpc-netty-shaded:1.74.0")
    implementation("io.grpc:grpc-protobuf:1.74.0")
    implementation("io.grpc:grpc-stub:1.74.0")
    compileOnly ("org.apache.tomcat:annotations-api:6.0.53") // necessary for Java 9+

    //JAVAFX
    implementation("org.openjfx:javafx-base:${javafx.version}:linux")
    implementation("org.openjfx:javafx-base:${javafx.version}:win")
    implementation("org.openjfx:javafx-base:${javafx.version}:mac")
    implementation("org.openjfx:javafx-graphics:${javafx.version}:linux")
    implementation("org.openjfx:javafx-graphics:${javafx.version}:win")
    implementation("org.openjfx:javafx-graphics:${javafx.version}:mac")
    implementation("org.openjfx:javafx-fxml:${javafx.version}:linux")
    implementation("org.openjfx:javafx-fxml:${javafx.version}:win")
    implementation("org.openjfx:javafx-fxml:${javafx.version}:mac")
    implementation("org.openjfx:javafx-controls:${javafx.version}:linux")
    implementation("org.openjfx:javafx-controls:${javafx.version}:win")
    implementation("org.openjfx:javafx-controls:${javafx.version}:mac")
    implementation("org.controlsfx:controlsfx:11.2.1")

    testImplementation("junit:junit:4.13.1")
    testImplementation("org.mockito:mockito-core:4.4.0")
}

tasks.named('jar', Jar) {
    manifest {
        attributes(
                'Implementation-Title':   baseName,
                'Implementation-Version': implementationVersion
        )
    }
}

tasks.register('layReleaseDirectoriesOut') {
    mkdir('build/release')
    mkdir('build/release/launch-scripts')
}

tasks.register('copyLaunchScripts', Copy) {
    dependsOn('layReleaseDirectoriesOut')
    from('launch-scripts')
    into('build/release/launch-scripts')
}

tasks.register('copyInstallScripts', Copy) {
    dependsOn('layReleaseDirectoriesOut')
    from('install-scripts')
    into('build/release')
}

tasks.register('appOpenbisDriveServiceJar', Jar) {
    dependsOn('build')
    dependsOn('copyLaunchScripts')

    manifest {
        attributes(
                'Implementation-Title':   baseName + '-service',
                'Implementation-Version': implementationVersion,
                'Main-Class': 'ch.openbis.drive.DriveAPIService'
        )
    }
    archiveBaseName = baseName + '-service'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from('build/libs/' + baseName + '-service.jar')
            into('build/release/launch-scripts')
        }
    }
}

tasks.register('appOpenbisDriveCmdLineJar', Jar) {
    dependsOn('build')
    dependsOn('copyLaunchScripts')

    manifest {
        attributes(
                'Implementation-Title': baseName + '-cmd-line',
                'Implementation-Version': implementationVersion,
                'Main-Class': 'ch.openbis.drive.DriveAPICmdLineApp'
        )
    }
    archiveBaseName = baseName + '-cmd-line'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from('build/libs/' + baseName + '-cmd-line.jar')
            into('build/release/launch-scripts')
        }
    }
}

tasks.register('appOpenbisDriveGuiJar', Jar) {
    dependsOn('build')
    dependsOn('copyLaunchScripts')

    manifest {
        attributes(
                'Implementation-Title' : baseName + '-gui',
                'Implementation-Version' : implementationVersion,
                'Main-Class' : 'ch.openbis.drive.gui.Launcher'
        )
    }
    archiveBaseName = baseName + '-gui'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from('build/libs/' + baseName + '-gui.jar')
            into('build/release/launch-scripts')
        }
    }
}

tasks.register('release', Tar) {
    dependsOn('copyInstallScripts')
    dependsOn('appOpenbisDriveServiceJar')
    dependsOn('appOpenbisDriveCmdLineJar')
    dependsOn('appOpenbisDriveGuiJar')

    archiveFileName = baseName + '-' + implementationVersion+ ".tar.gz"

    from 'build/release'
    compression = Compression.GZIP
}