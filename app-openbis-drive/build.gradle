plugins {
    id 'java-library'
    id 'application'
    id 'com.google.protobuf' version '0.9.5'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.74.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

evaluationDependsOn(':lib-json')
evaluationDependsOn(':api-data-store-server-java')

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

//tasks.register('compileAvroSchemas', Exec) {
//    executable "java"
//    args "-jar",
//            "util/avro-tools-1.12.0.jar",
//            "compile",
//            "protocol",
//            "./src/main/resources/avro/drive-api.avpr",
//            "./src/main/java"
//}
//
//build {
//    dependsOn("compileAvroSchemas")
//}

repositories {
    ivy {
        ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
        artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    compileOnly("org.projectlombok:lombok:1.18.38")
    testCompileOnly("org.projectlombok:lombok:1.18.38")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.38")

    api(project(":lib-json"))
    api(project(":api-data-store-server-java"))

    implementation("org.xerial:sqlite-jdbc:3.50.2.0")
    implementation("commons-cli:commons-cli:1.10.0")

    //GOOGLE PROTOBUF
    implementation("com.google.protobuf:protobuf-java:4.31.1")
    implementation("io.grpc:grpc-netty-shaded:1.74.0")
    implementation("io.grpc:grpc-protobuf:1.74.0")
    implementation("io.grpc:grpc-stub:1.74.0")
    compileOnly ("org.apache.tomcat:annotations-api:6.0.53") // necessary for Java 9+


    testImplementation("junit:junit:4.13.1")
    testImplementation("org.mockito:mockito-core:4.4.0")
}


tasks.named('jar', Jar) {
    manifest {
        attributes(
                'Implementation-Title':   'app-openbis-drive',
                'Implementation-Version': '0.0.1'
        )
    }
}

tasks.register('copyLaunchScripts', Copy) {
    from('launch-scripts')
    into('build/launch-scripts')
}

tasks.register('app-openbis-drive-service-jar', Jar) {
    dependsOn('build')
    dependsOn('copyLaunchScripts')

    manifest {
        attributes(
                'Implementation-Title':   'app-openbis-drive-service',
                'Implementation-Version': '0.0.1',
                'Main-Class': 'ch.openbis.drive.DriveAPIService'
        )
    }
    archiveBaseName = 'app-openbis-drive-service'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from('build/libs/app-openbis-drive-service.jar')
            into('build/launch-scripts')
        }
    }
}

tasks.register('app-openbis-drive-cmd-line-jar', Jar) {
    dependsOn('build')
    dependsOn('copyLaunchScripts')

    manifest {
        attributes(
                'Implementation-Title': 'app-openbis-drive-cmd-line',
                'Implementation-Version': '0.0.1',
                'Main-Class': 'ch.openbis.drive.DriveAPICmdLineApp'
        )
    }
    archiveBaseName = 'app-openbis-drive-cmd-line'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from('build/libs/app-openbis-drive-cmd-line.jar')
            into('build/launch-scripts')
        }
    }
}
// openbis-drive-VERSION.tar.gz
tasks.register('openbisDriveTar', Tar) {
    dependsOn([jar])
    archiveFileName = 'openbis-drive-' + project.ext.versionNumber + ".tar.gz"
    from jar.archiveFile
    into 'openbis-drive'
}

