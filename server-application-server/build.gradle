import org.gradle.api.tasks.bundling.Jar

evaluationDependsOn(':lib-commonbase')
evaluationDependsOn(':lib-common')
evaluationDependsOn(':api-openbis-java')
evaluationDependsOn(':api-openbis-javascript')
evaluationDependsOn(':api-data-store-server-javascript')
evaluationDependsOn(':api-data-store-server-java')
evaluationDependsOn(':lib-openbis-common')
evaluationDependsOn(':lib-authentication')
evaluationDependsOn(':lib-dbmigration')

apply from: '../build/javaproject.gradle'

configurations.create('testRuntimeFirst')
configurations.create('devRuntime')
configurations.create('asExecRuntime')

dependencies {
    api project(':lib-common'),
            project(':api-openbis-java'),
            project(':lib-openbis-commonbase'),
            project(':lib-openbis-common'),
            project(':lib-openbis-messages'),
            project(':lib-authentication'),
            project(':lib-mail'),
            project(':lib-json'),
            project(':lib-maintenance'),
            project(':lib-dbmigration'),
            project(':lib-messages-db'),
            project(':lib-two-phase-transaction'),
            project(':api-data-store-server-java'),
            project(':api-data-store-server-javascript'),
            'sencha:gxt:2.2.5',
            'jboss:javassist:3.28.0.GA',
            'hibernate:hibernate-core:5.2.14.Final',
            'fasterxml:classmate:1.3.0',
            'eclipse:jetty-deploy:10.0.25',
            'javax.activation:activation:1.1.1',
            'javax:servlet-api:4.0.1',
            'javax:mail:1.6.2',
            'springframework:spring-orm:5.3.39',
            'cisd:cisd-hotdeploy:13.01.0',
            'apache:poi-ooxml:3.17',
            'hibernate:hibernate-validator:4.3.2.Final',
            'unimi:fastutil:5.1.5',
            'apache:commons-collections4:4.1',
            'apache:commons-compress:1.8',
            'springframework:spring-context-support:5.3.39',
            'ehcache:ehcache:2.10.0',
            'jline:jline:0.9.94',
            'jsoup:jsoup:1.14.2',
            'org.apache.pdfbox:pdfbox:2.0.30',
            'org.apache.pdfbox:fontbox:2.0.30',
            'org.apache.pdfbox:xmpbox:2.0.30',
            'org.apache.pdfbox:pdfbox-io:3.0.0',
            'de.rototor.pdfbox:graphics2d:3.0.0',
            'com.openhtmltopdf:openhtmltopdf-core:1.0.10',
            'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'


    runtimeOnly 'slf4j:slf4j-jdk14:1.7.24',
            'apache:commons-fileupload:1.3.3'

    asExecRuntime files("../core-plugin-openbis/dist/core-plugins/imaging/1/as/api-listener/imaging-dataset-interceptor/lib/imaging-dataset-interceptor.jar"),
            files("../core-plugin-openbis/dist/core-plugins/imaging/1/as/services/imaging/lib/openBIS-imaging-technology.jar"),
            files("../core-plugin-openbis/dist/core-plugins/imaging-nanonis/1/as/services/imaging-nanonis/lib/premise-adapters.jar"),
            files("../core-plugin-openbis/dist/core-plugins/imaging-test/1/as/services/imaging-test/lib/imaging-test-adapters.jar")

    testImplementation project(path: ':lib-commonbase', configuration: 'tests'),
            project(path: ':lib-common', configuration: 'tests'),
            project(path: ':lib-dbmigration', configuration: 'tests'),
            project(path: ':lib-authentication', configuration: 'tests'),
            project(path: ':api-openbis-java', configuration: 'tests'),
            project(path: ':lib-openbis-common', configuration: 'tests'),
            project(path: ':lib-openbis-commonbase', configuration: 'tests'),
            project(path: ':lib-two-phase-transaction', configuration: 'tests'),
            project(path: ':api-data-store-server-javascript', configuration: 'tests'),
            'fjelmer:classycle:1.4.2',
            'testng:testng:6.8-CISD',
            'springframework:spring-test:5.3.39',
            'apache:commons-fileupload:1.3.3',
            'startnet:apgdiff:2.3',
            'hamcrest:hamcrest-integration:1.3',
            'hamcrest:hamcrest-library:1.3'

    testRuntimeFirst 'javax:servlet-api:4.0.1'

}

sourceSets.test.runtimeClasspath = configurations.testRuntimeFirst + sourceSets.test.runtimeClasspath


sourceSets {
    main {
        resources {
            srcDirs = ['source/java']
        }
    }
    test {
        resources {
            srcDirs = ['sourceTest/java']
        }
    }

    asExec {
        java {
            srcDirs = ['source/java']
        }
        resources {
            srcDirs = ['source/java']
        }

        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath + configurations.asExecRuntime + configurations.devRuntime
    }
}

task cleanDbSuite(type: Test) {
    useTestNG()
    systemProperty "ant.project.name", project.name
    maxHeapSize = "4096m"
    jvmArgs += [
            '-Duser.timezone=Europe/Zurich',
            '-Djava.util.logging.config.file=' + file('etc/jullogging.properties').absolutePath]
    testLogging.showStandardStreams = true
    ignoreFailures = true

    options.suites('sourceTest/java/tests_system_cleandb_excluding_authorization.xml')
    reports.html.destination = file("${project.buildDir}/reports/tests-cleandb")
}

task cleanDbSuiteProjectSamplesEnabled(type: Test) {
    useTestNG()
    systemProperty "ant.project.name", project.name
    maxHeapSize = "4096m"
    jvmArgs += [
            '-Duser.timezone=Europe/Zurich',
            '-Djava.util.logging.config.file=' + file('etc/jullogging.properties').absolutePath]
    testLogging.showStandardStreams = true
    ignoreFailures = true
    systemProperty "project-samples-enabled", 'true'

    options.suites('sourceTest/java/tests_system_cleandb_excluding_authorization_project_samples.xml')
    reports.html.destination = file("${project.buildDir}/reports/tests-cleandb-project-samples")
}

task testProjectSamplesEnabled(type: Test) {
    useTestNG()
    systemProperty "ant.project.name", project.name
    maxHeapSize = "4096m"
    jvmArgs += [
            '-Duser.timezone=Europe/Zurich',
            '-Djava.util.logging.config.file=' + file('etc/jullogging.properties').absolutePath]
    testLogging.showStandardStreams = true
    ignoreFailures = true
    systemProperty "project-samples-enabled", 'true'

    options.suites('sourceTest/java/tests_project_samples.xml')
    reports.html.destination = file("${project.buildDir}/reports/tests-project-samples")
}

// This task is here to make WebAppsPropertiesTest to work. It requires some data files
// to be present on the same directory than the class file itself.
task copyTestData(type: Copy, dependsOn: testClasses) {
    from "${project.projectDir}/sourceTest/java/ch/systemsx/cisd/openbis/generic/shared/basic"
    into "${project.buildDir}/classes/test/ch/systemsx/cisd/openbis/generic/shared/basic"
    include "*.properties"
}

task openBISDevelopmentEnvironmentASStart(type: JavaExec) {
    mainClass = 'ch.ethz.sis.openbis.generic.server.dev.EmbeddedJettyDevelopmentServer'
    classpath = sourceSets.asExec.runtimeClasspath
    jvmArgs(['-Dpython.path=../libraries/jython/jython-lib', '-Dlog.configuration=etc/logging.properties',
             '-Djavax.net.ssl.trustStore=dist/server/openBIS.keystore',
             '-Dorg.mortbay.util.FileResource.checkAliases=false',
             '--add-opens=java.base/java.util=ALL-UNNAMED', '--add-opens=java.base/java.lang=ALL-UNNAMED',
             '-Xmx4096M', '-ea'])
    args(['--classes', '../lib-commonbase/targets/gradle/classes/java/main',
          '--classes', '../lib-common/targets/gradle/classes/java/main',
          '--classes', '../lib-authentication/targets/gradle/classes/java/main',
          '--classes', '../lib-dbmigration/targets/gradle/classes/java/main',
          '--classes', '../lib-openbis-common/targets/gradle/classes/java/main',
          '--classes', '../api-openbis-java/targets/gradle/classes/java/main',
          '--classes', 'targets/gradle/classes/java/main', '--lib', 'targets/www/lib/', '--port', '8888', 'targets/www'])
}


/////////// AS JAVADOC ////////////////////

task serverApplicationServerJavadoc(type: Javadoc) {
    source project(':server-application-server').files('source/java').getAsFileTree().matching {
        include "**/ch/systemsx/cisd/openbis/generic/server/jython/api/v1/**/*.java"
        include "**/ch/systemsx/cisd/openbis/generic/shared/managed_property/api/**/*.java"
    }
    classpath = sourceSets.asExec.runtimeClasspath
    destinationDir = file("$buildDir/docs/javadoc-server-application-server")
}

task serverApplicationServerJavadocZip(type: Zip, dependsOn: serverApplicationServerJavadoc) {
    archiveBaseName = 'openbis-application-server-javadoc'
    from serverApplicationServerJavadoc.destinationDir
}



/////////// DYNAMIC PROPERTIES API ////////////////////


task dynamicApiJavadoc(type: Javadoc) {
    source project(':server-application-server').files('source/java').getAsFileTree().matching {
        include "ch/systemsx/cisd/openbis/generic/shared/hotdeploy_plugins/api/*.java"
    }
    classpath = sourceSets.asExec.runtimeClasspath
    destinationDir = file("$buildDir/docs/javadoc-dynamic-api")
}

task dynamicApiJavadocZip(type: Zip, dependsOn: dynamicApiJavadoc) {
    archiveBaseName = 'dynamic-api-javadoc'
    from dynamicApiJavadoc.destinationDir
}

testProjectSamplesEnabled.dependsOn(copyTestData)
cleanDbSuiteProjectSamplesEnabled.dependsOn(testProjectSamplesEnabled)
test.maxHeapSize = "10240m"
test.jvmArgs += [
        '-Duser.timezone=Europe/Zurich',
        '-Djava.util.logging.config.file=etc/jullogging.properties']
test.dependsOn(copyTestData)

apply from: 'elndev.gradle'
