configurations.create('gwt')
if (System.getProperty("openbis.development-build") != null) {
    configurations.gwt {
        transitive = false
    }

    dependencies {
        gwt 'reveregroup:gwt-image-loader:1.1.4',
                'google:gwt-dev:2.4',
                'google:gwt-user:2.4',
                'google:gwt-debug-panel:1.0',
                'sencha:gxt:2.2.5',
                'sis:sis-base:23.06.0',
                'fasterxml:jackson-annotations:2.9.10',
                project(':lib-common'),
                project(path: ':lib-common', configuration: 'archives'),
                project(path: ':api-openbis-java'),
                project(path: ':api-openbis-java', configuration: 'archives'),
                project(path: ':api-openbis-javascript'),
                project(path: ':lib-openbis-common'),
                project(path: ':lib-openbis-common', configuration: 'archives'),
                project(path: ':server-application-server', configuration: 'archives')
    }
}

def gwtTempPath = "${project.projectDir.absolutePath}/targets/www/gwt-temp"

task deleteGWT(type: Exec) {
    executable "rm"
    args "-rf", "${project.projectDir.absolutePath}/.gwt-cache", "${project.projectDir.absolutePath}/targets/www"
}

task prepareGWT(type: Copy, dependsOn: [deleteGWT, compileJava]) {
    from 'resource/server'
    into 'targets/www/WEB-INF'

    from(project.configurations.runtimeOnly.files) {
        into '../lib'
        exclude 'cisd-cifex*'
        exclude 'lib-common-*'
        exclude 'lib-authentication-*'
        exclude 'lib-dbmigration-*'
        exclude 'lib-openbis-common-*'
        exclude 'api-openbis-java-*'
    }

    from('source/java/service.properties') {
        into 'classes'
    }
}

task compileGWTToTempJDK8(type: JavaExec, dependsOn: prepareGWT) {
    // inputs.source
    sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir 'targets/www/gwt-temp'

    mainClass = 'com.google.gwt.dev.Compiler'
    maxHeapSize = '1024m'
    classpath {
        [
                sourceSets.main.java.srcDirs,
                project(":server-application-server").sourceSets.main.java.srcDirs,
                project(":server-application-server").sourceSets.main.output.resourcesDir,
                project(":server-application-server").sourceSets.main.output.classesDirs,
                project(":server-application-server").sourceSets.main.compileClasspath,
                project(":lib-common").sourceSets.main.java.srcDirs,
                project(":api-openbis-java").sourceSets.main.java.srcDirs,
                configurations.gwt
        ]
    }
    args = ['-war',
            'targets/www/gwt-temp',
            "ch.systemsx.cisd.openbis.OpenBISSafari",
            '-draftCompile',
            '-style', 'PRETTY',
            '-logLevel', 'INFO']
}

task compileGWT(type: Copy, dependsOn: compileGWTToTempJDK8) {
    from "targets/www/gwt-temp/ch.systemsx.cisd.openbis.OpenBIS"
    into "targets/www/openbis-test"
}

task untarGwt(type: Copy, dependsOn: prepareGWT) {
    def javaVersion = System.getProperty("java.version")
    def isJdk21 = javaVersion.startsWith("21")

    def gwtArchive = isJdk21
            ? "../core-plugin-openbis/resource/gwtJDK21.tar.gz"
            : "../core-plugin-openbis/resource/gwt.tar.gz"

    println("untarGwt archive used : " + gwtArchive)
    from tarTree(resources.gzip(file(gwtArchive)))
    into "${gwtTempPath}"
}

// should be removed if not used
task compileGWTToTempJDK11(type: Copy, dependsOn: untarGwt) {
    from "${project.projectDir.absolutePath}/source/java/ch/systemsx/cisd/openbis/public"
    into "${gwtTempPath}/ch.systemsx.cisd.openbis.OpenBIS"
}

task compileGWTToTemp(type: Copy, dependsOn: untarGwt) {
    from "${project.projectDir.absolutePath}/source/java/ch/systemsx/cisd/openbis/public"
    into "${gwtTempPath}/ch.systemsx.cisd.openbis.OpenBIS"
}

task copyV3Api(type: Copy, dependsOn: [compileGWTToTemp, ':api-openbis-javascript:bundleOpenbisStaticResources']) {
    from("${project(':api-openbis-javascript').projectDir}/src/v3")
    into "${gwtTempPath}/ch.systemsx.cisd.openbis.OpenBIS/resources/api/v3"
}

task openBISDevelopmentEnvironmentASPrepare(type: Copy, dependsOn: [copyV3Api]) {
    from "${gwtTempPath}/ch.systemsx.cisd.openbis.OpenBIS"
    into "targets/www/openbis-test"
    doLast {
        def Files = java.nio.file.Files
        def Paths = java.nio.file.Paths

        def targetLinkDirs = [
                Paths.get("${project(':server-application-server').projectDir}/source/core-plugins"),
                Paths.get("${project(':server-original-data-store').projectDir}/source/core-plugins")
        ]

        def sourceSymlinksMap = [
                "admin":   "${project(':ui-admin').projectDir}/src/core-plugins/admin",
                "eln-lims":"${project(':ui-eln-lims').projectDir}/src/core-plugins/eln-lims",
                "imaging": "${project(':core-plugin-openbis').projectDir}/dist/core-plugins/imaging"
        ]

        targetLinkDirs.each { targetLinkDir ->
            sourceSymlinksMap.each { linkName, targetPath ->
                def link = targetLinkDir.resolve(linkName)
                def target = Paths.get(targetPath)

                if (Files.exists(target)) {
                    Files.deleteIfExists(link)
                    println "Creating symlink: ${link} -> ${target}"
                    Files.createSymbolicLink(link, target)
                } else {
                    println "WARNING: Target for symlink '${linkName}' not found, skipping. Path: ${target}"
                }
            }
        }
    }
}
