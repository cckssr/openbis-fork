import org.gradle.api.tasks.bundling.Jar

evaluationDependsOn(':lib-commonbase')
evaluationDependsOn(':lib-common')
evaluationDependsOn(':api-openbis-java')
evaluationDependsOn(':api-openbis-javascript')
evaluationDependsOn(':lib-openbis-common')
evaluationDependsOn(':lib-authentication')
evaluationDependsOn(':lib-dbmigration')
evaluationDependsOn(':server-application-server')

apply from: '../build/javaproject.gradle'

configurations.create('testRuntimeFirst')
configurations.create('testRuntimeSecond')
configurations.create('datastoreExecRuntime')

dependencies {

    api(project(':server-application-server'))

    api project(':lib-commonbase'),
            project(':lib-common'),
            project(':lib-openbis-commonbase'),
            project(':lib-openbis-common'),
            project(':lib-data-store-commonbase'),
            project(':lib-data-store-common'),
            // lib-authentication and lib-dbmigration are required as they contain later versions of
            // some classes than cisd-cifex.jar. They need to be in classpath before cisd-cifex.jar
            project(':lib-authentication'),
            project(':lib-db'),
            project(':lib-dbmigration'),
            project(':lib-pathinfo-db'),
            project(':lib-pathinfo'),
            project(':lib-archiver-db'),
            project(':lib-archiver'),
            project(':lib-shuffling'),
            project(':lib-compress'),
            project(':lib-maintenance'),
            project(':lib-hierarchical-content'),
            'apache:ftpserver-core:1.0.6',
            'apache:sshd-sftp:2.7.0',
            'slf4j:slf4j-api:1.7.24',
            'slf4j:slf4j-jdk14:1.7.24',
            'bouncycastle:bcpg:1.59',
            'jfree:jfreechart:1.0.13',
            'cisd:cisd-cifex:r1550129411',
            'shriop:javacsv:2.0',
            'cisd:cisd-image-readers:r1553067167',
            'hjg:pngj:0.62',
            'apache:commons-fileupload:1.3.3',
            'docx4j:docx4j:6.1.2',
            'freva:ascii-table:1.2.0',
            'sis:sis-jhdf5:19.04.0'
            'jsoup:jsoup:1.14.2'
    api('dspace:xoai-data-provider:4.2.0'){
        exclude module: 'log4j'
    }

//    runtimeOnly 'bioformats:bioformats:6.5.1'
//    runtimeOnly('ome:formats-gpl:6.5.1'){
//        exclude group: 'ch.qos.logback', module: 'logbackâ€‘classic'
//    }


    // eln-lims Jython APIs dependencies
    datastoreExecRuntime files("../../../openbis-hvl.ethz.ch/deployed/core-plugins/hvl-dropbox/1/dss/dropboxes/hvl-dropbox/lib/jmatio-1.5.jar"),
            /* eln-lims */ files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/eln-lims-api/lib/zip4j_1.3.2.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/eln-lims-api/lib/plasmapper.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/eln-lims-api/lib/htmlcleaner-2.23.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/docx4j-3.3.3.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/ascii-table-1.1.0.jar"),
            files("../ui-eln-lims/src/dist/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/docxbuilder.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/jsoup-1.11.3.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/serializer-2.7.2.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/slf4j-api-1.7.21.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/exports-api/lib/xalan-2.7.2.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/zenodo-exports-api/lib/job-scheduler.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/zenodo-exports-api/lib/json-20250517.jar"),
            files("../ui-eln-lims/src/core-plugins/eln-lims/1/dss/reporting-plugins/password-reset-api/lib/persistentkeyvaluestore.jar")

    testImplementation project(path: ':lib-commonbase', configuration: 'tests'),
            project(path: ':lib-common', configuration: 'tests'),
            project(path: ':lib-dbmigration', configuration: 'tests'),
            project(path: ':lib-authentication', configuration: 'tests'),
            project(path: ':lib-hierarchical-content', configuration: 'tests'),
            project(path: ':api-openbis-java', configuration: 'tests'),
            project(path: ':lib-openbis-common', configuration: 'tests'),
            project(path: ':lib-openbis-commonbase', configuration: 'tests'),
            project(path: ':lib-data-store-commonbase', configuration: 'tests'),
            project(path: ':lib-logging', configuration: 'tests'),
            project(path: ':lib-hdf5', configuration: 'tests'),
            'fjelmer:classycle:1.4.2',
            'testng:testng:6.8-CISD'

    testImplementation(project(path: ':server-application-server', configuration: 'tests'))

    testRuntimeFirst 'javax:servlet-api:4.0.1',
            files("../core-plugin-openbis/dist/core-plugins/imaging/1/as/services/imaging/lib/openBIS-imaging-technology.jar"),
            files("../core-plugin-openbis/dist/core-plugins/imaging/1/as/api-listener/imaging-dataset-interceptor/lib/imaging-dataset-interceptor.jar")
}


sourceSets.test.runtimeClasspath = configurations.testRuntimeFirst + configurations.testRuntimeSecond + sourceSets.test.runtimeClasspath

sourceSets {
    main {
        resources {
            srcDirs = ['source/java']
        }
    }
    test {
        resources {
            srcDirs = ['source/java']
        }
    }

    datastoreExec {
        java {
            srcDirs = ['source/java']
        }
        resources {
            srcDirs = ['source/java']
        }

        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath + configurations.datastoreExecRuntime
    }
}

jar {
    duplicatesStrategy 'include'
    from('targets/dist') {
        include 'BUILD*INFO'
    }
    from('source/java') {
        include '*.xml'
    }
    from('source') {
        include 'sql/**/*.sql'
        into 'datastore_server'
    }
}

tasks.register('openBISDevelopmentEnvironmentDSSStart', JavaExec) {
    mainClass = 'ch.systemsx.cisd.openbis.dss.generic.DataStoreServer'
    classpath = sourceSets.datastoreExec.runtimeClasspath
    jvmArgs(['-ea', '-Dpython.path=../libraries/jython/jython-lib',
             '-Ddss.logging.configuration=etc/logging.properties',
             '-Dorg.eclipse.jetty.util.log.class=org.eclipse.jetty.util.log.StrErrLog'])
}

tasks.register('serverOriginalDataStoreJavadoc', Javadoc) {
    mustRunAfter ":core-plugin-openbis:copyOpenbisNgUiToCorePlugins"
    source project.files('source/java').getAsFileTree().matching {
        include "**/ch/systemsx/cisd/openbis/dss/generic/server/plugins/jython/api/**/*.java"
        include "**/ch/systemsx/cisd/openbis/dss/generic/shared/api/internal/**/*.java"
    }
    classpath = sourceSets.datastoreExec.runtimeClasspath
    destinationDir = file("$buildDir/docs/javadoc-server-original-dataStore")
}

tasks.register('serverOriginalDataStoreJavadocZip', Zip) {
    dependsOn serverOriginalDataStoreJavadoc
    archiveBaseName = 'openbis-original-data-store-javadoc'
    from serverOriginalDataStoreJavadoc.destinationDir
}

tasks.register('javadocTar', Tar) {
    dependsOn serverOriginalDataStoreJavadoc
    archiveFileName = "openbis-original-data-store-javadoc-${version}.tar.gz"
    from serverOriginalDataStoreJavadoc.destinationDir
}

/////////// DROPBOX API V2 ////////////////////

tasks.register('dropboxApiJar', Jar) {
    archiveBaseName = 'openbis-dropbox-api'
    includeEmptyDirs false
    from compileJava.outputs.getFiles().getAsFileTree().matching {
        include "ch/systemsx/cisd/etlserver/registrator/api/v2/*.class"
        include "ch/systemsx/cisd/etlserver/registrator/api/v2/impl/*.class"
        include "ch/systemsx/cisd/openbis/dss/generic/shared/api/**/*.class"
    }
}

tasks.register('dropboxApiZip', Zip) {
    dependsOn[dropboxApiJar]
    archiveBaseName = 'openbis-dropbox-api'
    from dropboxApiJar.archiveFile
    into 'openbis-dropbox-api'
}


tasks.register('dropboxApiJavadoc', Javadoc) {
    mustRunAfter ":core-plugin-openbis:copyOpenbisNgUiToCorePlugins"
    source project.files('source/java').getAsFileTree().matching {
        include "ch/systemsx/cisd/etlserver/registrator/api/v2/*.java"
        include "ch/systemsx/cisd/etlserver/registrator/api/v2/impl/*.java"
        include "ch/systemsx/cisd/openbis/dss/generic/shared/api/**/*.class"
    }
    classpath = sourceSets.datastoreExec.runtimeClasspath
    destinationDir = file("$buildDir/docs/javadoc-dropbox")
}

tasks.register('dropboxJavaDocZip', Zip) {
    dependsOn dropboxApiJavadoc
    archiveBaseName = 'openbis-dropbox-javadoc'
    from dropboxApiJavadoc.destinationDir
}

tasks.register('dropboxJavadocTar', Tar) {
    dependsOn dropboxApiJavadoc
    archiveFileName = "openbis-dropbox-javadoc-${version}.tar.gz"
    from dropboxApiJavadoc.destinationDir
}