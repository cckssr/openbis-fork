evaluationDependsOn(':lib-commonbase')
evaluationDependsOn(':lib-common')
evaluationDependsOn(':api-openbis-java')
evaluationDependsOn(':api-openbis-javascript')
evaluationDependsOn(':lib-openbis-common')
evaluationDependsOn(':lib-authentication')
evaluationDependsOn(':lib-dbmigration')
evaluationDependsOn(':server-application-server')
evaluationDependsOn(':server-original-data-store')

apply from: '../build/javaproject.gradle'

configurations.create('devRuntime')

dependencies {
    api project(':lib-common'),
            project(':server-original-data-store'),
            project(':lib-pathinfo'),
            'openhms:jackcess:1.2.2',
            'reveregroup:gwt-image-loader:1.1.4',
            'imagej:ij:1.43u'
            //'bioformats:bioformats:6.5.2'
//      api ('ome:formats-gpl:6.5.1') {
//            exclude group: 'ch.qos.logback', module: 'logbackâ€‘classic'
//          }


    devRuntime project(path: ':server-application-server', configuration: 'devRuntime')

    testImplementation project(path: ':lib-commonbase', configuration: 'tests'),
            project(path: ':lib-common', configuration: 'tests'),
            project(path: ':lib-dbmigration', configuration: 'tests'),
            project(path: ':lib-authentication', configuration: 'tests'),
            project(path: ':lib-hierarchical-content', configuration: 'tests'),
            project(path: ':api-openbis-java', configuration: 'tests'),
            project(path: ':lib-openbis-commonbase', configuration: 'tests'),
            project(path: ':lib-openbis-common', configuration: 'tests'),
            project(path: ':lib-data-store-commonbase', configuration: 'tests'),
            project(path: ':server-original-data-store', configuration: 'tests'),
            'fjelmer:classycle:1.4.2',
            'testng:testng:6.8-CISD',
            'springframework:spring-test:5.3.39',
            'startnet:apgdiff:2.3',
            'bioformats:bioformats:6.5.2'
    testImplementation(project(path: ':server-application-server', configuration: 'tests')) {
        exclude group: 'google', module: 'gwt-user'
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['source/java']
        }
    }
    test {
        resources {
            srcDirs = ['sourceTest/java']
        }
    }
}

jar {
    duplicatesStrategy 'include'
    from('../server-screening/source/sql') {
        into('/sql')
        include '**/*.sql'
    }
    from('../server-screening/source/java') {
        include 'screening-dssApplicationContext.xml'
    }
}

// This task is here to make TabularDataGraphCollectionConfigurationTest to work. It requires some data files
// to be present on the same directory than the class file itself.
task copyTestData(type: Copy, dependsOn: testClasses) {
    from "${project.projectDir}/sourceTest/java/ch/systemsx/cisd/openbis/dss/generic/server/graph"
    into "${project.buildDir}/classes/test/ch/systemsx/cisd/openbis/dss/generic/server/graph"
    include "*.properties"
}

task executeScreeningOpenBisDev(type: JavaExec) {
    mainClass = 'org.eclipse.jetty.runner.Runner'
    classpath = sourceSets.main.runtimeClasspath + configurations.devRuntime
    jvmArgs(['-Dpython.path=../libraries/jython/jython-lib', '-Dlog.configuration=../server-application-server/etc/logging.properties',
             '-Djavax.net.ssl.trustStore=../server-application-server/dist/server/openBIS.keystore',
             '-Dorg.eclipse.jetty.util.log.class=org.eclipse.jetty.util.log.StrErrLog',
             '-Dorg.mortbay.util.FileResource.checkAliases=false', '-Xmx2048M', '-ea'])
    args(['--classes', '../lib-commonbase/targets/gradle/classes/main',
          '--classes', '../lib-common/targets/gradle/classes/main',
          '--classes', '../lib-authentication/targets/gradle/classes/main',
          '--classes', '../lib-dbmigration/targets/gradle/classes/main',
          '--classes', '../lib-openbis-common/targets/gradle/classes/main',
          '--classes', '../api-openbis-java/targets/gradle/classes/main',
          '--classes', '../server-application-server/targets/gradle/classes/main',
          '--classes', 'targets/gradle/classes/main', '--lib', 'targets/www/lib/', '--port', '8888', 'targets/www'])
}

task executeScreeningDataStoreDev(type: JavaExec) {
    mainClass = 'ch.systemsx.cisd.openbis.dss.generic.DataStoreServer'
    classpath = sourceSets.main.runtimeClasspath + configurations.devRuntime +
            files("../core-plugin-openbis/dist/core-plugins/microscopy/3/dss/drop-boxes/MicroscopyDropbox/lib/MicroscopyReader_20190521113500.jar")
    jvmArgs(['-Dpython.path=../libraries/jython/jython-lib', '-Dlog.configuration=../server-original-data-store/etc/logging.properties',
             '-Djavax.net.ssl.trustStore=../server-original-data-store/dist/etc/openBIS.keystore',
             '-Dorg.eclipse.jetty.util.log.class=org.eclipse.jetty.util.log.StrErrLog',
             '-Dorg.mortbay.util.FileResource.checkAliases=false', '-Xmx2048M', '-ea'])
}

task systemTestSuite(type: Test) {
    useTestNG()
    options.suites('sourceTest/java/tests_system.xml')
    jvmArgs '-Xmx2048m'
    reports.html.destination = file("${project.buildDir}/reports/tests-system")
}

import org.gradle.api.tasks.bundling.Compression
import org.gradle.api.tasks.bundling.Tar

def gwtArchive = file("../core-plugin-openbis/resource/gwt.tar.gz")
def gwtTempDir = layout.buildDirectory.dir("gwt-temp-dir").get().asFile
def gwtArchiveOutput = file("../core-plugin-openbis/resource/gwtJDK21.tar.gz")

tasks.register("untarGwt", Copy) {
    from tarTree(resources.gzip(gwtArchive))
    into gwtTempDir

    doFirst {
        println("untarGwt archive used : " + gwtArchive)
        if (gwtTempDir.exists()) {
            println("Deleting existing temp dir: $gwtTempDir")
            gwtTempDir.deleteDir()
        }
    }


    onlyIf {
        gwtArchive.exists()
    }
}

tasks.register('verifyGwtCrc', JavaExec) {
    dependsOn tasks.named("untarGwt")
    group = 'verification'
    description = 'Verifies and fixes GWT serialization signatures'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'ch.systemsx.cisd.openbis.utils.gwt.GWTTypeCRC32VerifierAndFixer'

    doFirst {
        def rpcFileArg = gwtTempDir.absolutePath + "/ch.systemsx.cisd.openbis.OpenBIS/D92B876EF6D622EDAED05608D2D8D122.gwt.rpc"
        // Automatically pass gwtTempDir as sourceDir
        args gwtTempDir.absolutePath, rpcFileArg
        println("verifyGwtCrc args: ${args}")
    }
}



tasks.register("tarGwt", Tar) {
    dependsOn tasks.named("verifyGwtCrc")
    group = "build"
    description = "Re-packages the verified GWT output into a tar.gz archive"

    from(gwtTempDir)
    destinationDirectory.set(gwtArchiveOutput.parentFile)
    archiveFileName.set(gwtArchiveOutput.name)

    compression = Compression.GZIP
    archiveExtension.set("tar.gz")

    doFirst {
        println("Creating new GWT tarball at: ${gwtArchiveOutput}")
    }

    doLast {
        println "Fixing completed. Cleaning up temp dir: $gwtTempDir"
        if (gwtTempDir.exists()) {
            gwtTempDir.deleteDir()
        }
    }
}

if (System.getProperty("java.version").startsWith("21")) {
    tasks.register("patchGwtTypeSignaturesJdk21") {
        dependsOn tasks.named("tarGwt")
        group = "verification"
        description = "Fixes GWT Type IDs for Java 21 and re-packages the archive"

        def javaVersion = System.getProperty("java.version")

        doFirst {
            println "Fixing GWT serialization type IDs for Java version: $javaVersion"
        }

        doLast {
            println "Fixing completed and GWT archive re-packaged."
        }
    }
}


test.dependsOn(systemTestSuite)
test.dependsOn(copyTestData)

apply from: 'gwtdev.gradle'
