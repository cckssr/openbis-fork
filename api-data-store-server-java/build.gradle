evaluationDependsOn(':lib-json')

apply plugin: 'java'
apply plugin: 'application'

compileJava {
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.compilerArgs << '-parameters'
}

sourceCompatibility='11'
targetCompatibility='11'

repositories {
    ivy {
        ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
        artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
    mavenCentral()
}

dependencies {
    compileOnly 'lombok:lombok:1.18.38'
    annotationProcessor 'lombok:lombok:1.18.38'
    implementation project(':lib-json'),
            'lombok:lombok:1.18.38'
    testImplementation 'junit:junit:4.10',
            'hamcrest:hamcrest-core:1.3'
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'lombok' && details.requested.name == 'lombok') {
            // preserve whatever version they asked for
            details.useTarget(
                    group:   'org.projectlombok',
                    name:    'lombok',
                    version: details.requested.version
            )
            details.because("map shorthand lombok:lombok â†’ org.projectlombok:lombok")
        }
    }
}