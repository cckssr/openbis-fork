plugins {
    id 'java'
    id 'application'
    id 'io.quarkus' version '3.23.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

repositories {
    ivy {
        ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
        artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
    mavenCentral()
}

dependencies {
    annotationProcessor 'lombok:lombok:1.18.38'

    implementation 'lombok:lombok:1.18.38',
            'lombok:lombok:1.18.22',
            project(':lib-logging'),
            project(':lib-ro-crate'),
            project(':api-openbis-java'),
            project(':app-ro-crate-openbis')


    // Quarkus
    implementation enforcedPlatform("io.quarkus:quarkus-bom:3.23.0"), // Any dependencies coming from quarkus take precedence
            'io.quarkus:quarkus-resteasy:3.23.0'

    // Ro-crate library
    implementation 'edu.kit.datamanager:ro-crate-java:2.0.1',
            'com.fasterxml.jackson.core:jackson-core:2.18.1',
            'com.fasterxml.jackson.core:jackson-databind:2.18.1',
            'com.fasterxml.jackson.core:jackson-annotations:2.18.1',
            'com.networknt:json-schema-validator:1.5.7',
            'net.lingala.zip4j:zip4j:2.11.5'

    testImplementation 'testng:testng:6.8-CISD',
                        'io.rest-assured:rest-assured:5.5.5'
}

test {
    useTestNG()
}

mainClassName = "ch.ethz.sis.rocrateserver.startup.StartupMain"


task openBISDevelopmentEnvironmentROSStart(type: JavaExec) {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args(["../server-ro-crate/src/main/resources/service.properties"])
    jvmArgs(['--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
             '--add-opens=java.base/java.nio=ALL-UNNAMED',
             '-Dio.netty.tryReflectionSetAccessible=true',
             '-Xmx512M', '-ea'])
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClassName
        )
    }
}

task RoCrateServerZip(type: Zip, dependsOn: compileJava) {
    archiveBaseName = 'server-ro-crate'
    from(jar) {
        into('server-ro-crate/lib/')
    }
    from(configurations.runtimeClasspath) {
        into('server-ro-crate/lib/')
    }
    from('dist') {
        into 'server-ro-crate/'
    }
}